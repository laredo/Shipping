import "Leg.obs"

contract Transport {

    state Load;
    state InTransport {
        Leg@InTransit currentLeg;
    }
    state Unload {
        string destinationPort;
    };


    string transportName;
    string transportType;
    string originPort;
    LegList@Owned legList;

    Transport@Load(string pTransportName, string pTransportType, string pOriginPort, string date) {
        transportName = pTransportName;
        transportType = pTransportType;
        originPort = pOriginPort;
        legList = new LegList();
        ->Load;

        IO io = new IO();
        io.println("");
        io.print("* ");
        io.print(date);
        io.print(" - transport [Load] on [");
        io.print(pTransportName);
        io.print("], type [");
        io.print(pTransportType);
        io.print("] at port [");
        io.print(pOriginPort);
        io.println("]");
    }


    transaction getTransportName(Transport this) returns string {
        return transportName;
    }


    transaction getTransportType(Transport this) returns string {
        return transportType;
    }


    transaction getOriginPort(Transport this) returns string {
        return originPort;
    }

    transaction getDestinationPort(Transport@Unload this) returns string {
        return destinationPort;
    }

    transaction depart(Transport@Load >> InTransport this, string pdepartureDate, string port) {
        Leg newLeg = new Leg(this, pdepartureDate, port);
        [newLeg @ InTransit];
        ->InTransport(currentLeg = newLeg);

        IO io = new IO();
        io.println("");
        io.print("* ");
        io.print(pdepartureDate);
        io.print(" - leg [InTransit]. port [");
        io.print(port);
        io.println("]");
        io.print("* ");
        io.print(pdepartureDate);
        io.print(" - transport [InTransport]. port [");
        io.print(port);
        io.println("]");

    }

    transaction stopOver(Transport@InTransport this, string parrivalDate, string port) {
        currentLeg.setArrival(parrivalDate, port);
        [currentLeg @ Arrived];
        legList.append(currentLeg);
        [currentLeg @ Unowned];
        currentLeg = new Leg(this, "", "");

        IO io = new IO();
        io.println("");
        io.print("* ");
        io.print(parrivalDate);
        io.print(" - leg [Arrived]. port [");
        io.print(port);
        io.println("]");
        io.println("");
    }


    transaction layoverDepart(Transport@ InTransport this, string date, string port) {
        Leg newLeg = new Leg(this, date, port);
        ->InTransport(currentLeg = newLeg);

        IO io = new IO();
        io.println("");
        io.print("* ");
        io.print(date);
        io.print(" - leg [InTransit]. port [");
        io.print(port);
        io.println("]");
        io.println("");
    }


    transaction arrive(Transport@InTransport >> Unload this, string date, string port) {
        currentLeg.setArrival(date, port);
        [currentLeg @ Arrived];
        legList.append(currentLeg);
        [currentLeg @ Unowned];
        ->Unload(destinationPort = port);

        IO io = new IO();
        io.println("");
        io.print("* ");
        io.print(date);
        io.print(" - leg [Arrived]. port [");
        io.print(port);
        io.println("]");

        io.print("* ");
        io.print(date);
        io.print(" - transport [Unload]. port [");
        io.print(port);
        io.println("]");
    }

}



contract TransportList {
    state Empty;

    state HasNext {
        TransportList@Owned next;
        Transport@Unload value;
    }

    TransportList@Empty() {
        ->Empty;
    }


    transaction append(TransportList@Owned this, Transport@Unload >> Unowned ptr) {
        switch this {
            case HasNext {
                next.append(ptr);
            }
            case Empty {
                ->HasNext(value = ptr, next = new TransportList());
            }
        }
    }

    transaction length(TransportList@Owned this) returns int {
        switch this {
            case HasNext {
                return 1 + next.length();
            }
            case Empty {
                return 0;
            }
        }
    }


    transaction nth(TransportList@Owned this, int n) returns Transport@Unload {
        switch this {
            case HasNext {
                if (n == 0) {
                    return value;
                }
                else {
                    return next.nth(n - 1);
                }
            }
            case Empty {
                revert "Can't get the nth item from an empty list.";
            }
        }
    }
    


}

