import "Shipping/Leg.obs"

contract Transport {

    state Load;
    state InTransport {
        Leg@InTransit currentLeg;
    }
    state Unload;


    string transportName;
    string transportType;
    string originPort;
    string destinationPort;
    LegList@Owned legList;

    Transport@Load(string pTransportName, string pTransportType, string pOriginPort, string date) {
        transportName = pTransportName;
        transportType = pTransportType;
        originPort = pOriginPort;
        legList = new LegList();
        ->Load;

        System.println("");
        System.print("* ");
        System.print(date);
        System.print(" - transport [Load] on [");
        System.print(pTransportName);
        System.print("], type [");
        System.print(pTransportType);
        System.print("] at port [");
        System.print(pOriginPort);
        System.println("]");
    }


    transaction getTransportName(Transport this) returns string {
        return transportName;
    }


    transaction getTransportType(Transport this) returns string {
        return transportType;
    }


    transaction getOriginPort(Transport this) returns string {
        return originPort;
    }

    transaction getDestinationPort(Transport this) returns string {
        return destinationPort;
    }

    transaction depart(Transport@Load >> InTransport this, string pdepartureDate, string port) {
        Leg newLeg = new Leg(this, pdepartureDate, port);
        [newLeg @ InTransit];
        ->InTransport(currentLeg = newLeg);

        System.println("");
        System.print("* ");
        System.print(pdepartureDate);
        System.print(" - leg [InTransit]. port [");
        System.print(port);
        System.println("]");
        System.print("* ");
        System.print(pdepartureDate);
        System.print(" - transport [InTransport]. port [");
        System.print(port);
        System.println("]");

    }

    transaction stopOver(Transport@InTransport this, string parrivalDate, string port) {
        currentLeg.setArrival(parrivalDate, port);
        [currentLeg @ Arrived];
        legList.append(currentLeg);
        [currentLeg @ Unowned];
        currentLeg = new Leg(this, "", "");

        System.println("");
        System.print("* ");
        System.print(parrivalDate);
        System.print(" - leg [Arrived]. port [");
        System.print(port);
        System.println("]");
        System.println("");
    }


    transaction layoverDepart(Transport@ InTransport this, string date, string port) {
        Leg newLeg = new Leg(this, date, port);
        ->InTransport(currentLeg = newLeg);

        System.println("");
        System.print("* ");
        System.print(date);
        System.print(" - leg [InTransit]. port [");
        System.print(port);
        System.println("]");
        System.println("");
    }


    transaction arrive(Transport@InTransport >> Unload this, string date, string port) {
        currentLeg.setArrival(date, port);
        [currentLeg @ Arrived];
        legList.append(currentLeg);
        [currentLeg @ Unowned];
        ->Unload(destinationPort = port);

        System.println("");
        System.print("* ");
        System.print(date);
        System.print(" - leg [Arrived]. port [");
        System.print(port);
        System.println("]");

        System.print("* ");
        System.print(date);
        System.print(" - transport [Unload]. port [");
        System.print(port);
        System.println("]");
    }

}



contract TransportList {

    state Empty;
    asset state HasNext {
        TransportList@Owned next;
    }
    asset state NoNext;

    Transport@Unload value;

    TransportList@Empty() {
        ->Empty;
    }


    transaction append(TransportList@Owned this, Transport@Unload >> Unowned ptr) {
        switch this {
            case HasNext {
                next.append(ptr);
            }
            case Empty {
                ->HasNext(value = ptr, next = new TransportList());
            }
        }
    }

    transaction length(TransportList@Owned this) returns int {
        switch this {
            case HasNext {
                return 1 + next.length();
            }
            case Empty {
                return 0;
            }
        }
    }


    transaction nth(TransportList@Owned this, int n) returns Transport@Unload {
        if (n == 0) {
            return value;
        } else {
            switch this {
                case HasNext {
                    return next.nth(n - 1);
                }
                case Empty {
                    return value; // ideally, this should throw an exception
                }
            }
        }
    }


}

