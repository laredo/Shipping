contract Leg {

    state InTransit;
    state Arrived {
        string arrivalDate;
        string destinationPort;
    }

    string departureDate;
    string originPort;
    Transport@Unowned carrier;

    Leg@InTransit(Transport@Unowned t, string ddepartureDate, string doriginPort) {
        ->InTransit(departureDate = ddepartureDate, originPort = doriginPort, carrier = t);
    }

    transaction getDepartureDate(Leg@Owned this) returns string {
        return departureDate;
    }

    transaction getOriginPort(Leg@Owned this) returns string {
        return originPort;
    }

    transaction getCarrier(Leg@Owned this) returns Transport@Unowned {
        return carrier;
    }


    transaction setArrival(Leg@InTransit >> Arrived this, string date, string port) {
        ->Arrived(arrivalDate = date, destinationPort = port);
    }
}



contract LegList {

    state Empty;
    state HasNext {
        Leg@Arrived value;
        LegList@Owned next;
    }

    LegList@Empty() {
        ->Empty;
    }

    transaction append(LegList@Owned this, Leg@Arrived >> Unowned pLeg) {
        switch this {
            case HasNext {
                next.append(pLeg);
            }
            case Empty {
                ->HasNext(value = pLeg, next = new LegList());
            }
        }
    }


    transaction length(LegList@Owned this) returns int {
        switch this {
            case HasNext {
                return 1 + next.length();
            }
            case Empty {
                return 0;
            }
        }
    }


    transaction nth(LegList@Owned this, int n) returns Leg@Unowned {
        switch this {
            case HasNext {
                if (n == 0) {
                    return value;
                }
                return next.nth(n - 1);
            }
            case Empty {
                revert "Can't access the nth element of an empty list.";
            }
        }
        
    }

}

